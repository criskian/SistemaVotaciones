plugins {
    id 'java'
    id 'application'
}

group = 'com.votaciones'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':common')
    implementation 'org.postgresql:postgresql:42.7.2'
    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'org.slf4j:slf4j-api:2.0.12'
    implementation 'ch.qos.logback:logback-classic:1.4.14'
    implementation 'com.zeroc:ice:3.7.6'
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

application {
    mainClass = 'com.votaciones.portalwebconsulta.ui.ConsultaUI'
}

test {
    useJUnitPlatform()
}

// Tarea para ejecutar el servidor
task runServer(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.votaciones.portalwebconsulta.PortalWebConsultaServer'
    args = ['--server']
}

// Tarea para ejecutar el cliente
task runClient(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.votaciones.portalwebconsulta.PortalWebConsultaClient'
    args = ['--client']
}

run {
    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
    if (args.contains('--client')) {
        mainClass = 'com.votaciones.portalwebconsulta.ui.ConsultaUI'
    } else if (args.contains('--server')) {
        mainClass = 'com.votaciones.portalwebconsulta.PortalWebConsultaServer'
    }
}

jar {
    manifest {
        attributes(
            'Main-Class': 'com.votaciones.portalwebconsulta.ui.ConsultaUI',
            'Class-Path': configurations.runtimeClasspath.files.collect { it.name }.join(' ')
        )
    }
    
    // Include dependencies in the JAR
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
} 