plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
}

group = 'com.votaciones'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.postgresql:postgresql:42.6.0'
    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'org.slf4j:slf4j-api:2.0.11'
    implementation 'ch.qos.logback:logback-classic:1.4.14'
    implementation 'com.zeroc:ice:3.7.6'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
}

javafx {
    version = '17.0.2'
    modules = ['javafx.controls', 'javafx.fxml']
}

application {
    mainClass = 'com.votaciones.mainserver.MainServerIce'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

jar {
    manifest {
        attributes(
            'Main-Class': 'com.votaciones.mainserver.AdminMesaApp',
            'Class-Path': configurations.runtimeClasspath.files.collect { it.name }.join(' ')
        )
    }
    archiveBaseName = 'AdminMesa'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}

task runServer(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.votaciones.mainserver.MainServer'
}

task runClient(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.votaciones.mainserver.MainClient'
}

task runClientUI(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.votaciones.mainserver.VotingSystemClientUI'
}

task runSistemaGestionMesasUI(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.votaciones.mainserver.SistemaGestionMesasUI'
}

task generateIceStubs(type: Exec) {
    workingDir projectDir
    commandLine 'slice2java', '-I', 'src/main/slice', '--output-dir', 'src/main/java', 'src/main/slice/MainServer.ice'
}

compileJava.dependsOn generateIceStubs

run {
    standardInput = System.in
    if (project.hasProperty('mode')) {
        args = [project.property('mode')]
    }
} 