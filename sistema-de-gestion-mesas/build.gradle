plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
}

group = 'com.votaciones'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':common')
    implementation 'org.postgresql:postgresql:42.7.2'
    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'org.slf4j:slf4j-api:2.0.12'
    implementation 'ch.qos.logback:logback-classic:1.4.14'
    implementation 'com.zeroc:ice:3.7.10'
    implementation project(':reliable-message')
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'
    implementation project(':proxy-cache-db-ciudad')
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

javafx {
    version = '17.0.2'
    modules = ['javafx.controls', 'javafx.fxml']
}

application {
    mainClass = 'com.votaciones.mainserver.AdminMesaApp'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

jar {
    manifest {
        attributes(
            'Main-Class': 'com.votaciones.mainserver.AdminMesaApp'
        )
    }
    archiveBaseName = 'AdminMesa'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}

task runServer(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.votaciones.mainserver.MainServer'
}

task runClient(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.votaciones.mainserver.MainClient'
}

task runClientUI(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.votaciones.mainserver.VotingSystemClientUI'
}

task runSistemaGestionMesasUI(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.votaciones.mainserver.AdminMesaApp'
}

task generateIceStubs(type: Exec) {
    workingDir projectDir
    commandLine 'slice2java', '-I', 'src/main/slice', '--output-dir', 'src/main/java', 'src/main/slice/MainServer.ice'
}

compileJava.dependsOn generateIceStubs

run {
    standardInput = System.in
    if (project.hasProperty('mode')) {
        args = [project.property('mode')]
    }
} 